buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
    }
}
apply plugin: 'com.android.application'
repositories {
    jcenter()
}
android {
    buildToolsVersion '24.0.3'
    compileSdkVersion 24
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 24
        jackOptions {
            enabled true
        }
    }
    signingConfigs {
        cvoegtle {
            storeFile file("/Users/cv/Google Developer/cvoegtle.signing.jks")
            storePassword "insecure"
            keyAlias "cvoegtle"
            keyPassword "insecure"
        }

    }
    lintOptions {
        checkReleaseBuilds true
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.cvoegtle
            minifyEnabled false
            zipAlignEnabled true
            versionNameSuffix '-debug'
        }
        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.cvoegtle
            zipAlignEnabled true
            minifyEnabled false
        }

    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(outputFile.parent, "ww3.apk")
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    productFlavors {
    }
}

dependencies {
    compile 'com.android.support:support-v13:24.2.1'
}

import java.util.regex.Pattern

task('increaseVersionCode') << {
    def manifestFile = file("src/main/AndroidManifest.xml")
    def pattern = Pattern.compile("versionCode=\"(\\d+)\"")
    def manifestText = manifestFile.getText()
    def matcher = pattern.matcher(manifestText)
    matcher.find()
    def versionCode = Integer.parseInt(matcher.group(1))
    def manifestContent = matcher.replaceAll("versionCode=\"" + ++versionCode + "\"")
    manifestFile.write(manifestContent)
}

tasks.whenTaskAdded { task ->
    if (task.name == 'generateReleaseBuildConfig') {
        task.dependsOn 'increaseVersionCode'
    }
}
