buildscript {
    ext.kotlin_version = '1.1.3'
    repositories {
        jcenter()
        maven {
            url "https://maven.google.com"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    jcenter()
}
android {
    buildToolsVersion '25.0.3'
    compileSdkVersion 25
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
    }
    signingConfigs {
        cvoegtle {
            storeFile file("/Users/cv/Google_Developer/cvoegtle.signing.jks")
            storePassword "insecure"
            keyAlias 'cvoegtle'
            keyPassword "insecure"
        }
    }
    lintOptions {
        checkReleaseBuilds true
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            signingConfig signingConfigs.cvoegtle
            minifyEnabled false
            zipAlignEnabled true
            versionNameSuffix '-debug'
        }
        release {
            debuggable false
            jniDebuggable false
            signingConfig signingConfigs.cvoegtle
            zipAlignEnabled true
            minifyEnabled false
        }

    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                output.outputFile = new File(outputFile.parent, "wetterwolke.apk")
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
}

dependencies {
    compile 'com.android.support:support-core-utils:25.3.1'
    compile 'com.android.support:support-v13:25.3.1'
    compile 'com.google.android.gms:play-services-location:11.0.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

import java.util.regex.Pattern

task('increaseVersionCode') << {
    def manifestFile = file("src/main/AndroidManifest.xml")
    def pattern = Pattern.compile("versionCode=\"(\\d+)\"")
    def manifestText = manifestFile.getText()
    def matcher = pattern.matcher(manifestText)
    matcher.find()
    def versionCode = Integer.parseInt(matcher.group(1))
    def manifestContent = matcher.replaceAll("versionCode=\"" + ++versionCode + "\"")
    manifestFile.write(manifestContent)
}

tasks.whenTaskAdded { task ->
    if (task.name == 'generateReleaseBuildConfig') {
        task.dependsOn 'increaseVersionCode'
    }
}
